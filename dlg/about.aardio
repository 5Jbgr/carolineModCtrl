import win.ui;
import myutil.config;
import config;
import thread.manage;
import thread.semaphore;
import update;
/*DSG{{*/
var about = win.form(text="关于";right=527;bottom=359;border="thin";max=false;min=false;mode="popup")
about.add(
about_richedit={cls="richedit";left=232;top=8;right=520;bottom=232;autohscroll=false;autovscroll=false;bgcolor=15790320;db=1;dl=1;dr=1;dt=1;link=1;multiline=1;readonly=1;z=2};
download_button={cls="button";text="重新下载角色信息";left=264;top=328;right=520;bottom=352;z=4};
plus={cls="plus";left=8;top=8;right=228;bottom=228;background="\卡罗琳.png";db=1;dl=1;dt=1;z=1};
warningText={cls="edit";text="Edit";left=8;top=240;right=520;bottom=328;autohscroll=false;autovscroll=false;bgcolor=15790320;color=5734655;db=1;dl=1;dr=1;font=LOGFONT(h=-14);hidesel=1;multiline=1;readonly=1;z=3}
)
/*}}*/

var __config=myutil.config.get_config();
var _config=myutil.config.get_langconfig();
var _lang_other=_config.get_other("about");
table.mix(_lang_other,{
	title="关于";
	warningText='模组是作者的一片心血，请大家不要随意分享，\r\n也请大家不要去官方以及社区分享这些信息！！！\r\n不要把MOD截图发到官群和公开平台,否则会导致封号和MOD被堵死';
	redownload="重新下载角色信息";
});
table.mix(_lang_other.about_richedit,{
	version='\r\n版本：';
	newest_version='\r\n最新版本：';
	web='\r\n网址：';
	character_info='\r\n角色信息';
	loding="获取中";
	fail="获取失败";
	updating="更新中";
	update_success="更新成功";
});
about.text=_lang_other.title;
about.warningText.text=_lang_other.warningText;
about.download_button.text=_lang_other.redownload;

about.about_richedit.appendText({
	bold=true;
	text=config.__appName;
	point=9;
})

about.about_richedit.appendText({
	text='\r\n '++config.__appName_en;
	point=7;
})
about.about_richedit.appendText({
	bold=false;
	text=_lang_other.about_richedit.version;
	point=6;
	mask=1;
});
about.about_richedit.appendText(config.__version);
about.about_richedit.appendText(_lang_other.about_richedit.newest_version);
about.about_richedit.appendText(_lang_other.about_richedit.loding);
about.about_richedit.appendText(_lang_other.about_richedit.web);
about.about_richedit.appendText(config.__website);
about.about_richedit.appendText({
	bold=true;
	text=_lang_other.about_richedit.character_info;
	point=7;
})
about.about_richedit.appendText({
	text=_lang_other.about_richedit.version;
	point=6;
	mask=1;
});
about.about_richedit.appendText(string.slice(__config.ui.category_version,1,10));
about.about_richedit.appendText(_lang_other.about_richedit.newest_version);
about.about_richedit.appendText(_lang_other.about_richedit.loding);
about.about_richedit.appendText(_lang_other.about_richedit.web);
about.about_richedit.appendText(config.__website_category);


/*
import console;
console.open();
*/

var manage = thread.manage(3);
var semaphore = thread.semaphore(,1); 


check_app_update=function(about,loding,fail,semaphore){
	import update;
	var data = update.check_app_update();
	if(!about.about_richedit)return;
	semaphore.wait();
	var a,b = about.about_richedit.findText(loding,,1,-1); 
	about.about_richedit.setsel(a,b);
	if(data){
		about.about_richedit.replaceText(loding, string.slice(data[1].tag_name,2));
	}else {
		about.about_richedit.replaceText(loding,fail);
	}
	semaphore.release();
}

check_info_update=function(about,loding,fail,anchor_point,semaphore){
	import update;
	var data = update.check_info_update();
	if(!about.about_richedit)return;
	semaphore.wait();
	var a,b = about.about_richedit.findText(string.trim(anchor_point),,1,-1);
	a,b = about.about_richedit.findText(loding,,b,-1);
	about.about_richedit.setsel(a,b);
	if(data){
		about.about_richedit.replaceText(loding, string.slice(data.commit.sha,1,10));
	}else {
		about.about_richedit.replaceText(loding,fail);
	}
	semaphore.release();
	return data?data.commit.sha:null; 
}

download=function(...){
	import update;
	return update.download_info(...);
}



manage.createLite(
	check_app_update,
	about,
	_lang_other.about_richedit.loding,
	_lang_other.about_richedit.fail,
	semaphore
);
manage.create(
	check_info_update,
	about,
	_lang_other.about_richedit.loding,
	_lang_other.about_richedit.fail,
	_lang_other.about_richedit.character_info,
	semaphore
).onEnd=function(version){
	if(version and __config.ui.category_version!==version){
		var v_text=string.slice(__config.ui.category_version,1,10);
		var ci_text=string.trim(_lang_other.about_richedit.character_info);
		var u_text=_lang_other.about_richedit.updating;
		var a,b = about.about_richedit.findText(ci_text,,1,-1);
		var lang=__config.ui.language_download[__config.ui.language];
		a,b = about.about_richedit.findText(v_text,,b,-1);
		about.about_richedit.setsel(a,b);
		about.about_richedit.replaceText(v_text,string.format("%s(%s)",v_text,u_text));
		manage.create(download,version,lang).onEnd = function(r,...){
			if(!about.about_richedit)return;
			semaphore.wait();
			a,b = about.about_richedit.findText(ci_text,,1,-1);
			a,b = about.about_richedit.findText(u_text,,b,-1);
			about.about_richedit.setsel(a,b);
			if(r){
				var s_text = _lang_other.about_richedit.update_success;
				about.about_richedit.replaceText(u_text,s_text);
				__config.ui.category_version=version;
			}else {
				var f_text = _lang_other.about_richedit.fail; 
				about.about_richedit.replaceText(u_text,s_text);
			}
			semaphore.release();
		};
		manage.waitClose();
	}
};

about.download_button.oncommand = function(id,event){
	var version=__config.ui.category_version;
	var lang=__config.ui.language_download[__config.ui.language];
	manage.create(download,version,lang).onEnd = function(r,...){
		if(about.about_richedit){
			if(r){
				about.download_button.text=_lang_other.redownload+"("+_lang_other.about_richedit.update_success+")";
			}else {
				about.download_button.text=_lang_other.redownload+"("+_lang_other.about_richedit.fail+")"
			}
		}
	};
	manage.waitClose();
}

about.show();
manage.waitClose();
win.loopMessage();
return about;