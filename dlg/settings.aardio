import fonts.fontAwesome;
import win.ui;
import myutil.config;
import fsys.dlg.dir;
import console;
import debug.log;
import myutil.dlg;
/*DSG{{*/
var settings = win.form(text="设置";right=759;bottom=469)
settings.add(
about_button={cls="button";text="关于";left=648;top=432;right=728;bottom=456;db=1;dr=1;z=11};
client_combobox={cls="combobox";left=448;top=128;right=608;bottom=152;aw=1;dr=1;dt=1;edge=1;items={};mode="dropdown";z=6};
client_static={cls="static";text="客户端类型：";left=320;top=128;right=448;bottom=154;align="right";center=1;dl=1;dr=1;dt=1;font=LOGFONT(h=-14);notify=1;transparent=1;z=7};
copy2_radiobutton={cls="radiobutton";text="复制";left=80;top=264;right=152;bottom=288;db=1;dl=1;z=16};
copy_radiobutton={cls="radiobutton";text="复制";left=80;top=200;right=152;bottom=224;dl=1;dt=1;z=13};
game_static={cls="static";text="游戏目录：";left=24;top=48;right=152;bottom=74;align="right";center=1;dl=1;dt=1;font=LOGFONT(h=-14);transparent=1;z=2};
gamepath_plus={cls="plus";left=152;top=48;right=560;bottom=74;align="right";border={bottom=1;color=-6908266};dl=1;dr=1;dt=1;editable=1;font=LOGFONT(h=-13);textPadding={top=6;bottom=2};z=1};
hardlink_radiobutton={cls="radiobutton";text="硬链接";left=168;top=200;right=248;bottom=224;dl=1;dt=1;z=14};
language_combobox={cls="combobox";left=152;top=128;right=312;bottom=152;aw=1;dl=1;dt=1;edge=1;items={};mode="dropdown";z=5};
language_static={cls="static";text="语言：";left=24;top=128;right=152;bottom=154;align="right";center=1;dl=1;dt=1;font=LOGFONT(h=-14);notify=1;transparent=1;z=4};
localization={cls="checkbox";text="小开关";left=80;top=336;right=217;bottom=359;db=1;dl=1;z=19};
modctl_groupbox={cls="groupbox";text="模组管理";left=72;top=176;right=256;bottom=232;dl=1;dt=1;edge=1;z=12};
modim_groupbox={cls="groupbox";text="模组导入";left=72;top=240;right=256;bottom=296;db=1;dl=1;edge=1;z=15};
move_radiobutton={cls="radiobutton";text="移动";left=168;top=264;right=248;bottom=288;db=1;dl=1;z=17};
open_plus={cls="plus";text="打开";left=568;top=48;right=632;bottom=74;autohscroll=false;autovscroll=false;bgcolor=16775408;color=3947580;dr=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=7;top=2}};iconText='\uF07C';notify=1;textPadding={left=18};z=3};
zip_info_checkbox={cls="checkbox";text="显示解压信息";left=80;top=304;right=224;bottom=320;db=1;dl=1;z=18};
zip_open_plus={cls="plus";text="打开";left=568;top=88;right=632;bottom=114;autohscroll=false;autovscroll=false;bgcolor=16775408;color=3947580;dr=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=7;top=2}};iconText='\uF07C';notify=1;textPadding={left=18};z=10};
zip_path_plus={cls="plus";left=152;top=88;right=560;bottom=114;align="right";border={bottom=1;color=-6908266};dl=1;dr=1;dt=1;editable=1;font=LOGFONT(h=-13);textPadding={top=6;bottom=2};z=8};
zip_static={cls="static";text="7z目录：";left=24;top=88;right=152;bottom=114;align="right";center=1;dl=1;dt=1;font=LOGFONT(h=-14);notify=1;transparent=1;z=9}
)
/*}}*/

var cs_w=settings.client_static.width;
var cs_percent=(settings.client_static.right)/settings.width;
settings.adjust = function( cx,cy,wParam ) {
/*
	console.log(cx,cy);
	console.log(settings.right,settings.left);console.log(cs_percent);
	console.log(settings.client_static.right,settings.client_static.left);
*/
	settings.client_static.right=settings.width*cs_percent+6;
	settings.client_static.left=6+settings.width*cs_percent-cs_w*(settings.dpiScaleX:1);
};

var langs=table.array();
var _config = myutil.config.get_config(); 
var _lang = myutil.config.get_langconfig();
_lang.load_lang("settings",settings,{
	game_static="text";
	open_plus="text";
	language_static="text";
	client_static="text";
	zip_open_plus="text";
	zip_static="text";
	about_button="text";
	modctl_groupbox="text";
	modim_groupbox="text";
	copy_radiobutton="text";
	copy2_radiobutton="text";
	move_radiobutton="text";
	hardlink_radiobutton="text";
	zip_info_checkbox="text";
	localization="text";
});
table.mix(_config.ui,{
	manage="copy";
	"import"="copy";
	show_unzip_info=true;
});

settings.gamepath_plus.text=_config.path.game;
settings.zip_path_plus.text=_config.path.zip;
if(_config.ui[["import"]]==="copy"){
	settings.copy2_radiobutton.checked=true;
}else {
	settings.move_radiobutton.checked=true;
}
if(_config.ui[["manage"]]==="copy"){
	settings.copy_radiobutton.checked=true;
}else {
	settings.hardlink_radiobutton.checked=true;
}
settings.zip_info_checkbox.checked=_config.ui.show_unzip_info;

check_localization=function(root){
	if(not root or root==""){settings.localization.disabled=true;return;};
	var p=fsys.searchFile("localization.txt",root,true);
	if(p){
		var s=string.load(p);
		var state=string.match(s,"<localization>\s*?=\s*?(\d)");
		settings.localization.disabled=false;
		if(tonumber(state)==0){
			settings.localization.checked=false;
		}else {
			settings.localization.checked=true;
		}
	}else {
		debug.log.printf("[settings]:Can not find localization.txt at %s",p);
	}
}

check_localization(_config.path.game);


fsys.enum( _config.path.language, "*.*",
	function(dir,filename,fullpath,findData){ 
		if(filename){ 
           	//..console.log("发现文件："+filename,"完整路径："+fullpath)可使用 return false退出枚举文件过程
		}
		else{
			//..console.log( "发现目录：" + dir )
			table.push(langs,dir);
		}
	},
	false
);

settings.language_combobox.items=langs;
settings.language_combobox.selText=_lang.get_now_lang();
settings.client_combobox.items=table.map(_config.ui.client,lambda(v,k) v.name);
settings.client_combobox.selText=_config.ui.now_client;

settings.open_plus.oncommand =function(id,event){
	var path = fsys.dlg.dir(,,'请选择目录')
	if(path){
		debug.log.printf("[settings]:Change game path to %s",path);
		settings.gamepath_plus.text = path;
		_config.path.game=path;
		_config.path.install=io.joinpath(path,_config.path.mod);
		fsys.createDir(_config.path.install,false);
		check_localization(path);
	}	
}

settings.language_combobox.onOk = function(){ 
	var language=settings.language_combobox.selText;
	debug.log.printf("[settings]:Change language to %s",language);
	_config.ui.language=language;
	_lang.reset_lang();
}

settings.client_combobox.onOk = function(){ 
	var index=settings.client_combobox.selIndex;
	debug.log.printf("[settings]:Change client to %s",_config.ui.client[index].name);
	_config.ui.now_client=settings.client_combobox.selText;
	_config.path.mod=_config.ui.client[index].path;
	if(_config.path.game and _config.path.game!=""){
		_config.path.install=io.joinpath(_config.path.game,_config.path.mod);
		fsys.createDir(_config.path.install,false);
	}
}

/*
settings.zip_open_plus.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)
*/

settings.zip_open_plus.oncommand = function(id,event){
	var path = fsys.dlg.open("7z.exe|7z.exe",'请选择目录')
	if(path){
		settings.zip_path_plus.text = path;
		_config.path.zip=path;
	}
}

settings.about_button.oncommand = function(id,event){	
	var frmChild = settings.loadForm("\dlg\about.aardio");
	frmChild.show();
}

settings.copy_radiobutton.oncommand = function(id,event){
	table.assign(_config.ui,{
		manage="copy";
	});
}

settings.hardlink_radiobutton.oncommand = function(id,event){
	table.assign(_config.ui,{
		manage="hardlink";
	});
	var r=_lang.get_other("settings");
	table.mix(r,{ask_hardlink="使用硬链接请确保本软件和游戏在同一磁盘中！"});
	myutil.dlg.info_dlg(r.ask_hardlink);
}

settings.copy2_radiobutton.oncommand = function(id,event){
	table.assign(_config.ui,{
		"import"="copy";
	});
}

settings.move_radiobutton.oncommand = function(id,event){
	table.assign(_config.ui,{
		"import"="move";
	});
}

settings.zip_info_checkbox.oncommand = function(id,event){
	_config.ui.show_unzip_info=owner.checked;
}

settings.localization.oncommand = function(id,event){
	if(settings.localization.disabled)return;
	var p=fsys.searchFile("localization.txt",_config.path.game,true);
	if(p){
		var s=string.load(p);
		if(settings.localization.checked){
			s=string.replace(s,"^(.*?)(<localization>\s*?=\s*?)(\d)(.*?)$","\1\21\4");
		}else {
			s=string.replace(s,"^(.*?)(<localization>\s*?=\s*?)(\d)(.*?)$","\1\20\4");
		}
		debug.log.printf('[settings]:Change localization to %d \n\t%s',settings.localization.checked?1:0,s);
		string.save(p,s);
	}else {
		settings.localization.checked=!settings.localization.checked;
		debug.log.printf("[settings]:Can not find localization.txt at %s",p);
	}
}

settings.show();
win.loopMessage();
return settings;