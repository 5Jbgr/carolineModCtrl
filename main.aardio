//水平选项卡
import fonts.fontAwesome;
import win.ui;
import debug.log;
import myutil.config;
import myutil.dlg;
/*DSG{{*/
var mainForm = win.form(text="卡罗琳模组管理器";right=1100;bottom=623)
mainForm.add(
custom={cls="custom";left=0;top=32;right=1096;bottom=622;db=1;dl=1;dr=1;dt=1;z=4};
nav1={cls="plus";left=0;top=0;right=150;bottom=32;border={bottom=1;color=-16744448};dl=1;dt=1;font=LOGFONT(h=-14;name='微软雅黑');z=2};
nav2={cls="plus";left=149;top=0;right=299;bottom=32;border={bottom=1;color=-16744448};dl=1;dt=1;font=LOGFONT(h=-14;name='微软雅黑');z=3};
navEnd={cls="bkplus";left=-2;top=31;right=1096;bottom=32;bgcolor=32768;border=1;disabled=1;dl=1;dr=1;dt=1;z=1}
)
/*}}*/

debug.log.setPath("/log.log");
debug.log.checkSize(0x200000);

import modSQL;
import myutil;

global.onError = function( err,over ){ 
    if(!over){
        import debug;
        var stack = debug.traceback(,"调用栈",3);
        debug.log.print(stack,'\n',err)
    }
    
    if( _STUDIO_INVOKED ) return err;
}

myutil.dlg.init(mainForm);

import win.ui.tabs;
var tbs = win.ui.tabs(mainForm.nav1,mainForm.nav2);
tbs.clear(); //清除模板

var _config=myutil.config.get_config();
var _lang=myutil.config.get_langconfig();

//选项卡样式	
tbs.skin({
	foreground={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0xFFCCCCCC;
	};
	checked={
		foreground={ 
			default=0x00FFFFFF; 
		};
		border = { 
			default = {left=1;right=1;top=1;color=0xFF008000} 
			disabled = {left=1;right=1;top=1;color=0xFF008000} 
		}; 
	}
}); 

//下拉菜单按钮样式
tbs.dropButton.skin({   
	color = { 
		hover = 0xff99ffcc;
		active = 0xffff6666; 
		default = 0xff333333; 
	}
});

var _other=_lang.get_other("mainForm");
table.mix(_other,{
	modctl="模组控制";
	modmanage="模组管理";
	modsetting="设置";
});


init=function(){
	tbs.addItems({{text=_other.modctl;},{text=_other.modmanage;},{text=_other.modsetting;}});

	tbs.loadForm(1,"\dlg\mods_start.aardio");
	tbs.loadForm(2,"\dlg\mod_manage.aardio");
	tbs.loadForm(3,"\dlg\settings.aardio");
	if(not _config.path.game or _config.path.game==""){
		tbs.selIndex=3;
	}else {
		tbs.selIndex=1;
	}
}

init();

import myutil.modCtl;

myutil.modCtl.reset_category_callback=function(){
	tbs.clear();
	init();
}



tbs.onSelchange = function(idx,strip,form){
	if(form and form.refresh){
		form.refresh();
	}
}

//检查并设置管理员模式
myutil.quit=function(){
	mainForm.close();
	win.loopMessage();
}
myutil.before_quit=function(){
	myutil.config.save_config();
	modSQL.back_up_db();
}
myutil.set_admin();

mainForm.show();

var frmChild = mainForm.loadForm("\dlg\about.aardio");
frmChild.show();

win.loopMessage();

myutil.before_quit();
